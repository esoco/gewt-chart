buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7' }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6' }
}
apply plugin: 'gwt-base'

apply plugin: 'java'
apply plugin: 'osgi'

project.group   = 'de.esoco'
project.version = '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies
{
	if (project(':gewt').getProjectDir().exists())
	{
		compile project(':gewt')
	}
	else
	{
		compile 'de.esoco:gewt:1.+'
	}

	compile 'com.googlecode.gwt-charts:gwt-charts:0.9.10'
    testCompile 'junit:junit:4.+'
}

gwt {
    gwtVersion='2.7.0'
    modules 'de.esoco.GewtChart'
}

// sources are need for the GWT compiler
jar { from sourceSets.main.allSource }

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

bintray {
	user = project.hasProperty('bintrayUser') ? 
				project.property('bintrayUser') : 
				System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? 
				project.property('bintrayApiKey') : 
				System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true

	pkg {
		repo = 'sdack'
		name = project.name
		version {
			name = project.version
	    }
    }
}
